#version 430 core

layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

uniform sampler3D u_velocityField;
uniform sampler3D u_pressureField;

layout (rgba32f, binding = 2) uniform writeonly image3D u_writeTexture;

uniform vec3 u_gridSize;

void main()
{
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);
    float h = 1.0 / u_gridSize.x;
    float inv_h = 0.5 / h;

    float p_right = texelFetch(u_pressureField, coord + ivec3(1, 0, 0), 0).r;
    float p_left  = texelFetch(u_pressureField, coord + ivec3(-1, 0, 0), 0).r;
    float p_up    = texelFetch(u_pressureField, coord + ivec3(0, 1, 0), 0).r;
    float p_down  = texelFetch(u_pressureField, coord + ivec3(0, -1, 0), 0).r;
    float p_front = texelFetch(u_pressureField, coord + ivec3(0, 0, 1), 0).r;
    float p_back  = texelFetch(u_pressureField, coord + ivec3(0, 0, -1), 0).r;

    vec3 vel = texelFetch(u_velocityField, coord, 0).xyz;

    vel.x -= inv_h * (p_right - p_left);
    vel.y -= inv_h * (p_up - p_down);
    vel.z -= inv_h * (p_front - p_back);

    imageStore(u_writeTexture, coord, vec4(vel, 0.0));
}