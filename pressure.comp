#version 430 core
layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

uniform sampler3D u_pressure;
uniform sampler3D u_divergence;

layout (rgba32f, binding = 2) uniform writeonly image3D u_writeTexture;

void main()
{
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);

    // Get original divergence value
    float b_val = texelFetch(u_divergence, coord, 0).r;

    // Get neighbor pressure values from last iteration
    float p_left   = texelFetch(u_pressure, coord + ivec3(-1,  0,  0), 0).r;
    float p_right  = texelFetch(u_pressure, coord + ivec3( 1,  0,  0), 0).r;
    float p_down   = texelFetch(u_pressure, coord + ivec3( 0, -1,  0), 0).r;
    float p_up     = texelFetch(u_pressure, coord + ivec3( 0,  1,  0), 0).r;
    float p_back   = texelFetch(u_pressure, coord + ivec3( 0,  0, -1), 0).r;
    float p_front  = texelFetch(u_pressure, coord + ivec3( 0,  0,  1), 0).r;
    
    float neighbor_sum = p_left + p_right + p_down + p_up + p_back + p_front;

    // p_new = (divergence + neighbor_sum) / 6.0
    float p_new = (b_val + neighbor_sum) * (1.0 / 6.0);
    
    imageStore(u_writeTexture, coord, vec4(p_new, 0.0, 0.0, 0.0));
}