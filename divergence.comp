#version 430 core
layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

uniform sampler3D u_velocityField;
layout (rgba32f, binding = 2) uniform writeonly image3D u_writeTexture;

uniform vec3 u_gridSize;

void main()
{
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);
    float h = 1.0 / u_gridSize.x; // Grid cell size

    float vel_right = texelFetch(u_velocityField, coord + ivec3(1, 0, 0), 0).x;
    float vel_left  = texelFetch(u_velocityField, coord + ivec3(-1, 0, 0), 0).x;
    float vel_up    = texelFetch(u_velocityField, coord + ivec3(0, 1, 0), 0).y;
    float vel_down  = texelFetch(u_velocityField, coord + ivec3(0, -1, 0), 0).y;
    float vel_front = texelFetch(u_velocityField, coord + ivec3(0, 0, 1), 0).z;
    float vel_back  = texelFetch(u_velocityField, coord + ivec3(0, 0, -1), 0).z;

    // div = (dvx/dx) + (dvy/dy) + (dvz/dz)
    float divergence = -0.5 * h * (vel_right - vel_left + vel_up - vel_down + vel_front - vel_back);
    
    imageStore(u_writeTexture, coord, vec4(divergence, 0.0, 0.0, 0.0));
}