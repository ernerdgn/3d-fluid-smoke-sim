#version 430 core

layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

uniform sampler3D u_x;
uniform sampler3D u_b;

layout (rgba32f, binding = 2) uniform writeonly image3D u_writeTexture;

uniform float u_alpha;
uniform float u_rBeta;

void main()
{
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);
    
    vec4 b_val = texelFetch(u_b, coord, 0);
    
    vec4 x_left   = texelFetch(u_x, coord + ivec3(-1,  0,  0), 0);
    vec4 x_right  = texelFetch(u_x, coord + ivec3( 1,  0,  0), 0);
    vec4 x_down   = texelFetch(u_x, coord + ivec3( 0, -1,  0), 0);
    vec4 x_up     = texelFetch(u_x, coord + ivec3( 0,  1,  0), 0);
    vec4 x_back   = texelFetch(u_x, coord + ivec3( 0,  0, -1), 0);
    vec4 x_front  = texelFetch(u_x, coord + ivec3( 0,  0,  1), 0);
    
    vec4 neighbor_sum = x_left + x_right + x_down + x_up + x_back + x_front;

    vec4 new_val = (b_val + u_alpha * neighbor_sum) * u_rBeta;
    
    imageStore(u_writeTexture, coord, new_val);
}